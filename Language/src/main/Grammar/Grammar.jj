
options
{
    DEBUG_PARSER=false;
    DEBUG_TOKEN_MANAGER=false;
    LOOKAHEAD = 1;
}

PARSER_BEGIN(Parser)

package parser;

import nodes.*;
import nodes.primitives.*;
import nodes.arithmetic.*;
import nodes.relation.*;
import nodes.references.*;
import nodes.logic.*;
import nodes.functions.*;
import nodes.conditional.*;
import state.*;
import types.*;
import java.io.PrintStream;
import java.util.List;
import java.util.ArrayList;


public class Parser {
	PrintStream out;

	public void setOutputStream(PrintStream out)
    {
    	this.out = out;
    }
}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\r"
| "\n"
|  <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
|  <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

TOKEN :
{
  < DOT :  "." > |
  < COMMA : "," > |
  < ADD : "+" > |
  < SUB : "-" > |
  < MULT : "*" > |
  < DIV : "/" > |
  < EXCL : "!" > |
  < TILDE : "~" > |
  < CONJ : "&&" > |
  < EAGER_CONJ : "&" > |
  < DISJ : "||" > |
  < EAGER_DISJ : "|" > |
  < EQUALS : "=" > |
  < ASSIGNMENT : ":=" > |
  < EQEQ : "==" > |
  < EXCL_EQUALS : "!=" > |
  < GREATER : ">" > |
  < LESS : "<" > |
  < GR_EQUALS : ">=" > |
  < LE_EQUALS : "<=" > |
  < ARROW : "->" > |
  < SEMICOLON: ";" > |
  < DOUBLE_SEMICOLON: ";;" > |
  < COLON: ":" > |
  < LPAR : "(" > |
  < RPAR : ")" > |
  < LCUR : "{" > |
  < RCUR : "}" >
//  |
//  < STAT_ENDS: (<SEMICOLON> | <NL>)>
//  |
}

//Reserved words
TOKEN : {
	< LET: "let" > |
	< END: "end" > |
	< IN: "in" > |
	< TRUE: "true" > |
   	< FALSE: "false" > |
   	< NEW: "new" > |
   	< AS: "as" > |
   	< IF: "if" > |
   	< THEN: "then" > |
   	< ELSE: "else" > |
   	< WHILE: "while" > |
	< DO: "do" > |
	< FUN: "fun" > |
   	< INSTANCEOF: "instanceof" > |
   	< PRINTLN: "println" > |
   	< PRINT: "print" >
}

//Types
TOKEN : {
   	< INT: "int" > |
   	< FLOAT: "float" > |
   	< BOOLEAN: "boolean" > |
   	< ANY: "any" > |
   	< VOID: "void" > |
   	< REFERENCE: "ref" >
}

TOKEN : {
	< INTEGER_LITERAL: (<DIGIT>) + > |
	< FLOAT_LITERAL: (<DIGIT>)+ "." (<DIGIT>)+ > |
	< IDENTIFIER: <LETTER> (<LETTER> | <DIGIT> | "_")*> |
	< #LETTER: ["a"-"z","A"-"Z"]> |
	< #DIGIT: ["0"-"9"]>
}

ASTNode Start():
{
//	List<ASTNode> nodes = new ArrayList<>();
	ASTNode node, node2;
}
{
//   (node = Statment() ((<STAT_ENDS> | <EOF>) Statment())* (<EOF>)?
//   {return node;}
//   )?
    node = Sequence() <DOUBLE_SEMICOLON>
    {return node;}

}

ASTNode Sequence() :
{
	ASTNode node, node2;
}
{
	node = Statement() ( <SEMICOLON> node2 = Statement()
    	{
        	node = new ASTSequence(node,node2);
    	}
    )*
    {return node;}
}

/*
 * Statement syntax
 */

ASTNode Statement() :
{
	ASTNode node;
}
{
	(
		node = Expression()
		|
		node = While()
		|
		node = Println()
	)
    { return node; }
}

ASTStatement While() :
{
	ASTNode cond, body;
}
{
	<WHILE> cond = Expression() <DO> body = Sequence() <END>
	{ return new ASTWhile(cond,body); }
}


/*
 * Expression syntax
 */

ASTNode Expression() :
{
    ASTNode node;
}
{
    node=Assignment()
    { return node; }
}


ASTNode Assignment() :
{
    ASTNode node,node2;
}
{
    node = Disjunction() (LOOKAHEAD(1) <ASSIGNMENT> node2 = Disjunction()
    	{
			node = new ASTAssignment(node,node2);
 		}
    )*
    { return node; }
}

ASTNode Disjunction() :
{
	Token op;
    ASTNode node,node2;
}
{
    node = Conjunction() (LOOKAHEAD(1)(op = <DISJ> | op = <EAGER_DISJ>) node2 = Conjunction()
    	{
		 	switch(op.kind)
         	{
         		case EAGER_DISJ:
 					node = new ASTEagerOr(node,node2);
 					break;
 				case DISJ:
 					node = new ASTOr(node,node2);
 					break;
         	}
 		}
    )*
    { return node; }
}

ASTNode Conjunction() :
{
	Token op;
    ASTNode node,node2;
}
{
    node = Equality() (LOOKAHEAD(1)(op = <CONJ> | op = <EAGER_CONJ>) node2 = Equality()
    	{
		 	switch(op.kind)
         	{
         		case EAGER_CONJ:
 					node = new ASTEagerAnd(node,node2);
 					break;
 				case CONJ:
 					node = new ASTAnd(node,node2);
 					break;
         	}
 		}
    )*
    { return node; }
}

ASTNode Equality() :
{
    Token op;
    ASTNode node,node2;
}
{
    node = Comparison() (LOOKAHEAD(1)( op=<EQEQ> | op=<EXCL_EQUALS> )  node2=Comparison()
    	{
			switch(op.kind)
   			{
   				case EQEQ:
   					node = new ASTEqual(node,node2);
   					break;
   			    case EXCL_EQUALS:
   			    	node = new ASTNotEqual(node,node2);
   			    	break;
   			}
   		}
    )*
    { return node; }
}

ASTNode Comparison() :
{
    Token op;
    ASTNode node,node2;
}
{
    node = AddSubExpression() (LOOKAHEAD(1) ( op=<GREATER> | op=<LESS> | op=<GR_EQUALS> | op=<LE_EQUALS> )  node2=AddSubExpression()
    	{
			switch(op.kind)
   			{
   				case GREATER:
   					node = new ASTGreater(node,node2);
   					break;
   				case GR_EQUALS:
   					node = new ASTGreaterEqual(node,node2);
   					break;
   			    case LESS:
   			    	node = new ASTLess(node,node2);
   			    	break;
   			    case LE_EQUALS:
   			    	node = new ASTLessEqual(node,node2);
   			    	break;
   			}
   		}
    )?
    { return node; }
}

ASTNode AddSubExpression() :
{
    Token op;
    ASTNode node, node2;
}
{
    node = MultDivExpression()
    (
    	LOOKAHEAD(1)
    ( op=<ADD> | op=<SUB> )  node2=MultDivExpression()
    	{
			if (op.kind == ADD)
				node = new ASTAddition(node,node2);
			else
				node = new ASTSubtraction(node,node2);
		}
    )*
    { return node; }
}

ASTNode MultDivExpression() :
{
    Token op;
    ASTNode node, node2;
}
{
	node = AsExpression()
    (
    	LOOKAHEAD(1)( op=<MULT> | op=<DIV> ) node2 = AsExpression()
		{
			if (op.kind == MULT)
    	    	node = new ASTMultiplication(node,node2);
			else
				node = new ASTDivision(node,node2);
		}
    )*
    { return node; }
}

IType NumberType() :
{
	IType t;
}
{
	(
		<FLOAT> { t = FloatType.value; } |
		<INT> {t = IntType.value; }
	)
    { return t; }
}


ASTNode AsExpression() :
{
	IType type = null;
    ASTNode node;
}
{
	node = PrefixUnaryExpression() (LOOKAHEAD(1) <AS> type = NumberType() )?
	{
		if (type==null)
    	   	return node;
		else
    	    return new ASTAsType(node,type);
	}
}

Token PrefixUnaryOperator() :
{
    Token t;
}
{
    ( t = <SUB> | t =<TILDE> | t = <EXCL>)
    {return t;}
}

ASTNode PrefixUnaryExpression():
{
    Token op;
    List<Token> ops = new ArrayList<>();
    ASTNode node;
}
{
    (op = PrefixUnaryOperator() { ops.add(op); } )* node = Fact()
    {
    	for(Token t: ops)
    	{
    		switch(t.kind)
            {
            	case SUB:
            		node = new ASTNegate(node);
            		break;
            	case TILDE:
            		node = new ASTNot(node);
            		break;
                case EXCL:
                	node = new ASTDereference(node);
                	break;
            }
    	}
    	return node;
	}
}

ASTNode ApplyFunction(ASTNode function) :
{
	List<ASTNode> arguments;
}
{
	<LPAR> arguments = FunctionArguments() <RPAR>
	{ return new ASTApplyFunction(function,arguments); }
}

List<ASTNode> FunctionArguments() :
{
	List<ASTNode> arguments = new ArrayList<>();
	ASTNode argument;
}
{
	(
		argument = Sequence() { arguments.add(argument); }
		(< COMMA > argument = Sequence() { arguments.add(argument); })*
	)?

	{ return arguments; }
}

ASTNode Fact() :
{
    ASTNode node;
}
{
    (
        node = Literal()
        |
        node = Let()
        |
        node = TernaryExpression()
        |
        node = parenthesizedSequence()
        |
        node = Identifier()
        |
        node = NewReference()
        |
        node = Function()

    )
    {
        return node;
    }
}

List<Parameter> FunctionParameters() :
{
	List<Parameter> parameters = new ArrayList<>();
	Token identifier;
	IType type;
}
{
	(identifier = <IDENTIFIER> <COLON> type = Type()  { parameters.add(new Parameter(identifier.image,type)); }
	(<COMMA> identifier = <IDENTIFIER> <COLON> type = Type() { parameters.add(new Parameter(identifier.image,type)); })*)?
	{ return parameters; }
}

ASTNode Function() :
{
    List<Parameter> parameters;
    ASTNode body;
}
{

	<FUN>
		parameters = FunctionParameters()
	<ARROW>
		body = Sequence()
	<END>
	{ return new ASTFunction(parameters,body); }
}

ASTNode TernaryExpression() :
{
	ASTNode cond,pos,neg;
}
{
	<IF> cond = Expression() <THEN> pos = Sequence() <ELSE> neg = Sequence() <END>
	{ return new ASTTernary(cond,pos,neg); }
}

ASTNode Literal() :
{
    Token literal;
}
{
   literal = <INTEGER_LITERAL>
   { return new ASTInteger(Integer.parseInt(literal.image)); }
   |
   literal = <FLOAT_LITERAL>
   { return new ASTFloat(Float.parseFloat(literal.image)); }
   |
   (literal = <TRUE> | literal = <FALSE> )
   { return new ASTBoolean(Boolean.parseBoolean(literal.image)); }
}

ASTNode NewReference() :
{
	ASTNode exp;
}
{
	<NEW> exp = Disjunction()
	{return new ASTReference(exp);}
}

IType PrimitiveType() :
{
	IType t;
}
{
	(
		t = NumberType()
		|
		< BOOLEAN > { t = BooleanType.value; }

	)
    { return t; }
}


FunctionType functionType():
{
	IType t;
	List<IType> params = new ArrayList<>();
}
{
	<LPAR> ( t = Type() { params.add(t); } (<COMMA> t = Type() { params.add(t); } )*)? <RPAR> t = Type()
	{ return new FunctionType(t,params); }
}

IType Type() :
{
	IType t;
}
{
	(
		t = PrimitiveType()
		|
		< ANY > { t = AnyType.value; }
		|
		< VOID > { t = VoidType.value; }
		|
		<REFERENCE> t = Type() { t = new ReferenceType(t); }
		|
		t = functionType()


	)
	{ return t; }
}

ASTNode Identifier() :
{
    Token id;
    ASTNode identifier = null, opFun = null;
}
{
    id = <IDENTIFIER>
    (
    	{ identifier = new ASTIdentifier(id.image); }
        opFun = ApplyFunction(identifier)
    )?
    {
    	if(opFun == null)
        	return new ASTIdentifier(id.image);
       	return opFun;
    }
}

Binding Binding() :
{
    Token id;
    ASTNode sequence;
    IType type;
}
{
    id=<IDENTIFIER> <COLON> type = Type() <EQUALS> sequence = Sequence()
    { return new Binding(id.image,sequence,type); }
}

List<Binding> BindingsList() :
{
    Binding d;
    List<Binding> list = new ArrayList<>();
}
{
    d=Binding() {list.add(d);} ((<COMMA>)? d= Binding() {list.add(d);})*
    {return list;}

}

ASTNode Let() :
{
    List<Binding> list;
    ASTNode body;
}
{
    <LET>
        list = BindingsList()
    <IN>
        body = Sequence()
    <END>
    { return new ASTLet(list,body); }
}

ASTNode Println() :
{
    ASTNode exp;
}
{
	( <PRINTLN> | <PRINT> ) exp = Disjunction()
   { return new ASTPrintln(exp,out); }
}

ASTNode parenthesizedSequence() :
{
    ASTNode node;
}
{
    <LPAR>  node= Sequence()  <RPAR> { return node; }
}
